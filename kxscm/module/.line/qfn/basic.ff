// @author David Han
//
// @kind function
//
// @fileOverview Create a basic line chart.
//
// @example Create and render a line chart specification:
// .qp.go[600;400;] .line.basic.f01[]
//
// @returns {table} a GG specification (for example, ``.qp.point[t; `x; `y; ::]``)

{[]
  xfmt:{n:`long$last string x;$[0=n mod 2;x;`]};
  yfmt:{`$string[0.1*floor 0.5+1000*x],"%"};
  
  .qp.title["IEX Market Share Since Its Inception"]
  .qp.stack (
    .qp.line[.line.iexMktShare;`quarter;`mktshr;]
       .qp.s.geom[`fill`decorations!(`blue;0b)]
      ,.qp.s.labels[`x`y!(`Quarter;`$"Market Share (%)")]
      ,.qp.s.scale[`x;.gg.scale.format[xfmt;.gg.scale.categorical[]]]
      ,.qp.s.scale[`y;.gg.scale.format[yfmt;] .gg.scale.linear];
    .qp.point[.line.iexMktShare;`quarter;`mktshr;]
       .qp.s.geom[`colour`fill`size`shape`strokewidth!(`red;`white;3;`circle;1)]
      ,.qp.s.labels[`x`y!(`Quarter;`$"Market Share (%)")]
    )
  }
